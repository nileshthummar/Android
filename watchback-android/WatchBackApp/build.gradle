buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// FindBugs:
apply plugin: 'findbugs'
// Command for execution: ./gradlew --stacktrace --debug cleanBuildFindBugs
task findbugs(type: FindBugs) {
    description 'Find-bugs'
    group 'verification'
    //excludeFilter = file("$project.rootDir/tools/rules-findbugs.xml")
    classes = fileTree("$project.buildDir/intermediates/javac/prodDebug/compileProdDebugJavaWithJavac/classes/com/watchback/android")
    source = fileTree('src/main/java')
    effort 'max'
    reportLevel = "high"
    classpath = files()

    reports {
        xml.enabled = false
        html.enabled = true
//        html.destination = "$project.buildDir/outputs/findbugs/findbugs.html"
    }
}
task cleanBuildFindBugs(type: GradleBuild) {
    tasks = ['clean', 'assembleProdDebug', 'findbugs']
}

// PMD:
apply plugin: 'pmd'
// Command for execution: ./gradlew --stacktrace --debug cleanRunPmd
task pmd(type: Pmd) {
    description 'PMD'
    group 'verification'
    ruleSetFiles = files("rules-pmd.xml")
    source = fileTree('src/main/java')
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
//        html.destination = "$project.buildDir/outputs/pmd/pmd.html"
    }
}
task cleanRunPmd(type: GradleBuild) {
    tasks = ['clean', 'pmd']
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.google.com' }
    maven {

        credentials {
            username 'perkdeploy'
            password 'dyEWHjykNUspKFkR'
        }
        url 'http://maven.viggle.com/nexus/content/repositories/releases/'
    }
    maven {
        url "http://repo.brightcove.com/releases"
    }
    // For BottomNavigationViewEx library (required since Google doesn't allow disabling
    // shift-animation for BottomNavigationView yet)
    maven { url "https://jitpack.io" }

    maven { url "https://comscore.bintray.com/Analytics" }
    // AppsFlyer SDK:
    mavenCentral()

    // Singular SDK
    maven { url 'http://maven.singular.net/' }
}

android {
    compileSdkVersion 28

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.watchback2.android"
        // Set to 19 (Android 4.4) as Brightcove SDKs don't actively support older Android versions
        minSdkVersion 19

        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"

        // Enabling multidex support.
        multiDexEnabled true

        // Specify fabric release notes and email ids for Beta distribution
        ext.betaDistributionNotifications = true
        ext.betaDistributionReleaseNotesFilePath = "release_notes.txt"
        ext.betaDistributionGroupAliases = "PerkQA"

        // Read secret keys from secret keys properties
        Properties secretKeysProperties = new Properties()
        secretKeysProperties.load(new FileInputStream("WatchBackApp/secretKeys.properties"))

        // Add secret keys in build configuration
        buildConfigField "com.perk.request.auth.SecretKeys", "secretKeys", "new com.perk.request.auth.SecretKeys(${secretKeysProperties.getProperty('com.perk.apiSecretKey')}, ${secretKeysProperties.getProperty('com.perk.authClientId')}, ${secretKeysProperties.getProperty('com.perk.authProductIdentifier')}, ${secretKeysProperties.getProperty('com.perk.authProdSecretKey')}, ${secretKeysProperties.getProperty('com.perk.authDevSecretKey')}, ${secretKeysProperties.getProperty('com.perk.apiDevBaseUrl')}, ${secretKeysProperties.getProperty('com.perk.apiProdBaseUrl')})"

        // Singular SDK:
        buildConfigField "String", "singularApiKey", "\"perkmobile\""
        buildConfigField "String", "singularSecret", "\"ycoJRRsZ\""

        // Brightcove APIs:
        buildConfigField "String", "brightcoveAccountId", "${secretKeysProperties.getProperty('com.watchback.brightcove.accountId')}"
        buildConfigField "String", "brightcoveApiPolicyKey", "${secretKeysProperties.getProperty('com.watchback.brightcove.apiHeaderPolicyKey')}"
        buildConfigField "String", "brightcoveSearchApiPolicyKey", "${secretKeysProperties.getProperty('com.watchback.brightcove.searchHeaderPolicyKey')}"

        // Enable vector-drawables with support library for Kitkat & lower versions:
        // More details: https://chris.banes.me/2016/02/25/appcompat-vector/#enabling-the-flag
        vectorDrawables.useSupportLibrary = true

        // Java 8
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    // Defines the signing configuration used by us for signing build
    //noinspection GroovyAssignabilityCheck
    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "watchbackRelease.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    flavorDimensions "api"
    productFlavors {
        dev {
            buildConfigField "String", "buildMode", "\"dev\""
            // AppsFlyer:
            buildConfigField "String", "appsFlyerKey", "\"Y9nn3tzNT5mbQVPDDjUTWf\""
            buildConfigField "String", "appsFlyerApiToken", "\"f5ef705d-f3cc-479f-a7b3-493d0c07d6d9\""
            // LeanPlum:
            buildConfigField "String", "leanplumAppId", "\"app_qf4q41qGqqQQJtdQ3kaxXOS9BfQH8NjSZvA2eWMgfl0\""
            // development key
            buildConfigField "String", "leanplumDevKey", "\"dev_Vl4NuHW6nBoxvEyWFKCgdmVmvvqd8fAsRHMeGhiKH5g\""
            // production key
            buildConfigField "String", "leanplumProdKey", "\"prod_zuyXx47mVHQxRG7e2XTDqgj8Odyx0VEcF9TWRFsKWNc\""
        }
        prod {
            buildConfigField "String", "buildMode", "\"prod\""
            // AppsFlyer:
            // TODO: Update these for PROD if there are different values for it
            buildConfigField "String", "appsFlyerKey", "\"Y9nn3tzNT5mbQVPDDjUTWf\""
            buildConfigField "String", "appsFlyerApiToken", "\"f5ef705d-f3cc-479f-a7b3-493d0c07d6d9\""
            // LeanPlum:
            buildConfigField "String", "leanplumAppId", "\"app_qf4q41qGqqQQJtdQ3kaxXOS9BfQH8NjSZvA2eWMgfl0\""
            // production key
            buildConfigField "String", "leanplumProdKey", "\"prod_zuyXx47mVHQxRG7e2XTDqgj8Odyx0VEcF9TWRFsKWNc\""
            // development key
            buildConfigField "String", "leanplumDevKey", "\"dev_Vl4NuHW6nBoxvEyWFKCgdmVmvvqd8fAsRHMeGhiKH5g\""
        }
    }

    buildTypes {
        // TODO: Enable Proguard
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        // Used for development builds
        debug {
            debuggable true
            ext.alwaysUpdateBuildId = false
        }
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {

    }

    lintOptions {
        checkReleaseBuilds true
        // Check for errors in release builds, but continue the build even when errors are found:
        // TODO: Set this to true once we have a clean code base, free of un-required resources/classes
        abortOnError false
    }

}

ext {
    // Brightcove SDK version -set to 6.+ as per docs here:
    // https://support.brightcove.com/quick-start-build-app-using-brightcove-native-sdk-android#Create_a_project
    //brightcoveLibraryVersion = '6.+'
    brightcoveLibraryVersion = '6.7.0'

    leanplumVersion = '+'
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.google.android.gms:play-services-base:16.1.0"
    implementation "com.google.android.gms:play-services-gcm:16.1.0"
    implementation "com.google.android.gms:play-services-location:16.0.0"
    implementation "com.google.android.gms:play-services-ads:17.2.1"
    implementation "com.google.android.gms:play-services-cast:16.2.0"

    // Firebase for Leanplum push messaging:
    implementation "com.google.firebase:firebase-messaging:18.0.0"
    implementation "com.google.firebase:firebase-core:16.0.9"

    // Support library components:
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.mediarouter:mediarouter:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    annotationProcessor 'androidx.annotation:annotation:1.0.2'
    // For Brightcove exoplayer2
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'

    // Lifecycles only (no ViewModel or LiveData).
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.0.0"

    ///Perk start
    implementation('com.perk:request:1.7.3') { transitive = true }
    implementation('com.perk:webview:2.7.0') { transitive = true }
    ///Perk End

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }

    implementation('com.facebook.android:facebook-android-sdk:[4, 5)')

    implementation "com.infstory:proguard-annotations:1.0.2" //for can't access keep error

    implementation("com.brightcove.player:exoplayer2:$brightcoveLibraryVersion") {
        transitive = true
    }
    //implementation "com.brightcove.player:android-freewheel-plugin:$brightcoveLibraryVersion"
    implementation "com.brightcove.player:android-cast-plugin:$brightcoveLibraryVersion"

    // CircleImageView library
    implementation 'de.hdodenhof:circleimageview:3.0.0'

    // RoundedImageView library
    implementation 'com.makeramen:roundedimageview:2.3.0'

    // library to load images
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    // Glide transformations library:
    implementation 'jp.wasabeef:glide-transformations:4.0.1'

    implementation 'com.github.ittianyu:BottomNavigationViewEx:2.0.2'

    // Commons lang library
    implementation 'org.apache.commons:commons-lang3:3.9'

    // ExpiringMap: For caching purposes(thread-safe ConcurrentMap implementation that expires
    // entries -which is the only reason to use this rather than ConcurrentHashMap)
    implementation 'net.jodah:expiringmap:0.5.8'

    // Library for showing indicators for ViewPager:
    implementation 'me.relex:circleindicator:2.1.2'

    // AppsFlyer SDK:
    //noinspection GradleDynamicVersion
    implementation 'com.appsflyer:af-android-sdk:4+@aar'
    // Commented below since Singular SDK already includes this
    //implementation'com.android.installreferrer:installreferrer:1.0'

    ///comScore
    //noinspection GradleDynamicVersion
    implementation "com.comscore:android-analytics:5.+"
    // implementation ('com.comscore.sdk:android:5.6.0.171205')
    implementation 'com.adobe.mobile:adobeMobileLibrary:4.17.0'

    // Leanplum SDK:
    //noinspection GradleDynamicVersion
    implementation "com.leanplum:leanplum-fcm:$leanplumVersion"

    // Leanplum Location services for location-based messaging.
    //noinspection GradleDynamicVersion
    implementation "com.leanplum:leanplum-location:$leanplumVersion"

    // Facebook Shimmer-loading animation:
    implementation 'com.facebook.shimmer:shimmer:0.3.0'

    //Moat
    //implementation(name: 'NBX-moat-mobile-app-kit-2.4.6', ext: 'aar')

    ///Freewheel
    //implementation(name: 'FWAdManager6_24_0', ext: 'aar')

    // Singular SDK:
    implementation 'com.singular.sdk:singular_sdk:9.0.4'
}
// Include for FCM (Firebase Cloud messaging) - for Leanplum
apply plugin: 'com.google.gms.google-services'
